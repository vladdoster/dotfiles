#autoload
# vim: set et:ft=zsh:sw=2:st=2:ts=2:tw=100:

emulate -L zsh
setopt EXTENDED_GLOB

zmodload zsh/zutil || return 1

opt_help=
cli_usage=("Usage: +brew-link-kegs [-h|--help]")
zparseopts -D -F -K -- {h,-help}=opt_help || return 1
[[ -z "$opt_help" ]] || {
    builtin print -l $cli_usage && return
}

brew_keg_only=($(brew info --installed --json=v1 | jq --raw-output --compact-output  'map(select(.keg_only == true)) | map(.name) | @sh'))
if (( ${#brew_keg_only} > 0 )); then
    local HOMEBREW_PREFIX="$(brew --prefix)"
    for keg in ${(@on)brew_keg_only}; do
        PATH="${HOMEBREW_PREFIX}/opt/${(Q)keg}/bin:${PATH}"
        builtin print -P -- "%F{green}==>%f [+] ${(Q)keg}"
    done
fi

local f_path util
local -a gnu_paths=(/opt/homebrew/opt /usr/local/opt)
for f_path in $gnu_paths; do
    if [[ -e $f_path ]]; then
        builtin print -P -- "%F{cyan}==>%f searching ${(Q)f_path}"
        local -a gnu_tools=($(find $f_path -follow -type d -path '*gnubin*' -print))
        if (( ${#gnu_tools} > 0 )); then
            for util in ${(@on)gnu_tools}; do
                PATH="${(Q)util}:${PATH}"
                util_name="$(print -n -- ${util}/*(:t))"
                builtin print -P -- "%F{green}==>%f [+] ${(Q)util_name}"
            done
        fi
    fi
done

typeset -Ugx PATH
